SET(AB_LINK_LIBS)

###WXWIDGETS###

INCLUDE(FindwxWidgets)

SET(wxWidgets_USE_LIBS base core gl media)
FIND_PACKAGE(wxWidgets REQUIRED base core gl media)
INCLUDE(${wxWidgets_USE_FILE})
ADD_DEFINITIONS(-DwxUSE_NO_MANIFEST=1)
SET(AB_LINK_LIBS ${AB_LINK_LIBS} ${wxWidgets_LIBRARIES})

###ANTLR###

FILE(GLOB ANTLR_SRCS antlr/src/*.c)
FILE(GLOB ANTLR_HDRS antlr/include/*.h)
ADD_LIBRARY(antlr STATIC
  ${ANTLR_SRCS}
  ${ANTLR_HDRS}
)

INCLUDE_DIRECTORIES(
antlr/include
)

INCLUDE(FindJava)

SET(PARSER_SRCS
${CMAKE_CURRENT_SOURCE_DIR}/pcdkParser.c
#${CMAKE_CURRENT_SOURCE_DIR}/pcdkParser.h
${CMAKE_CURRENT_SOURCE_DIR}/pcdkLexer.c
#${CMAKE_CURRENT_SOURCE_DIR}/pcdkLexer.h
)

SET_SOURCE_FILES_PROPERTIES(
${PARSER_SRCS}
PROPERTIES
GENERATED TRUE
LANGUAGE CXX
)

ADD_CUSTOM_COMMAND(OUTPUT ${PARSER_SRCS}
COMMAND ${JAVA_RUNTIME} -cp antlrworks-1.2.3.jar org.antlr.Tool pcdk.g
DEPENDS pcdk.g
WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
COMMENT "antlr'ing grammar"
)

SOURCE_GROUP("Resource Files" FILES AppBuilder.rc)
SOURCE_GROUP("Grammar" FILES pcdk.g)

SET(AB_LINK_LIBS ${AB_LINK_LIBS} antlr)

###GLEW###

INCLUDE_DIRECTORIES(glew/include)
ADD_DEFINITIONS(-DGLEW_STATIC)
ADD_SUBDIRECTORY(glew)
SET(AB_LINK_LIBS ${AB_LINK_LIBS} glew)

###Open AL###
INCLUDE(FindOpenAL)
INCLUDE_DIRECTORIES(${OPENAL_INCLUDE_DIR})
SET(AB_LINK_LIBS ${AB_LINK_LIBS} ${OPENAL_LIBRARY})

###ALUT###
INCLUDE_DIRECTORIES(alut/include)
ADD_SUBDIRECTORY(alut)
SET(AB_LINK_LIBS ${AB_LINK_LIBS} alut)

###AppBuilder###

FILE(GLOB AppBuilder_SRCS *.cpp)
FILE(GLOB AppBuilder_HDRS *.h)

ADD_EXECUTABLE(AppBuilder
  ${AppBuilder_SRCS}
  ${AppBuilder_HDRS}
  ${PARSER_SRCS}
  AppBuilder.rc
  pcdk.g
)
TARGET_LINK_LIBRARIES(AppBuilder ${AB_LINK_LIBS})

SET_TARGET_PROPERTIES(AppBuilder
PROPERTIES
WIN32_EXECUTABLE TRUE)
